import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'dagger.hilt.android.plugin'
    id 'org.jmailen.kotlinter' version '3.7.0'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.livetl.android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 270
        versionName "6.4.0-beta01"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'variant'
    productFlavors {
        playstore {
            dimension 'variant'
        }
//        fdroid {
//            dimension 'variant'
//            applicationIdSuffix '.fdroid'
//        }
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    lintOptions {
        disable "MissingTranslation"
        abortOnError false
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.core:core-ktx:1.8.0-alpha04'

    // Jetpack Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation 'androidx.navigation:navigation-compose:2.5.0-alpha02'

    // Networking
    final ktor_version = '1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    // Image loading
    final coil_version = '1.4.0'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    final accompanist_version = '0.23.1'
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0'

    // OSS licenses
    implementation "com.mikepenz:aboutlibraries-core:$about_libs_version"
}

// Requires the submodules to already be initialized, i.e.:
// git submodule update --init --recursive
task buildExtensionSource {
    doLast {
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "install"
        }
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "build", "android"
        }
    }
}
task copyExtensionArtifacts(dependsOn: buildExtensionSource, type: Copy) {
    from '../extension/LiveTL/build'
    into './src/main/assets'
    include '**/*'
}

kotlinter {
    experimentalRules = true
}
preBuild.dependsOn(formatKotlin)

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += [
            "-Xopt-in=kotlin.Experimental",
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi",
            "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi",
            "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi",
            "-Xopt-in=coil.annotation.ExperimentalCoilApi",
            "-Xopt-in=com.google.accompanist.navigation.material.ExperimentalMaterialNavigationApi",
    ]
}
