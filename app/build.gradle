import org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.10'
    id 'org.jmailen.kotlinter' version '3.3.0'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.livetl.android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
        viewBinding true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    // DI
    final koin_version = '2.2.2'
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-compose:$koin_version"

    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'androidx.core:core-ktx:1.5.0-beta02'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    // Jetpack Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-viewbinding:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation 'androidx.navigation:navigation-compose:1.0.0-alpha08'

    // Video player
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'

    implementation 'com.github.arkon:android-youtubeExtractor:76e4e8c'

    // Networking
    final ktor_version = '1.5.1'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'

    // Image loading
    implementation 'io.coil-kt:coil:1.1.1'
    implementation 'dev.chrisbanes.accompanist:accompanist-coil:0.6.1'

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.3.4'
}

kotlinter {
    experimentalRules = true
}
preBuild.dependsOn(formatKotlin)

tasks.withType(AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-Xopt-in=kotlin.Experimental",
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xuse-experimental=androidx.compose.foundation.ExperimentalFoundationApi",
    ]
}