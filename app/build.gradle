import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'dagger.hilt.android.plugin'
    id 'org.jmailen.kotlinter' version '3.10.0'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.livetl.android"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 280
        versionName "8.0.0-beta03"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'variant'
    productFlavors {
        playstore {
            dimension 'variant'
        }
//        fdroid {
//            dimension 'variant'
//            applicationIdSuffix '.fdroid'
//        }
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.core:core-ktx:1.8.0'

    // Jetpack Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0-alpha13"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation 'androidx.navigation:navigation-compose:2.5.0-rc02'

    // Networking
    final ktor_version = '1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'

    // Image loading
    final coil_version = '2.1.0'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    final accompanist_version = '0.24.12-rc'
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0'

    // OSS licenses
    implementation "com.mikepenz:aboutlibraries-compose:$about_libs_version"
}

// Requires the submodules to already be initialized, i.e.:
// git submodule update --init --recursive
task buildExtensionSource {
    doLast {
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "install"
        }
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "build:android"
        }
    }
}
task copyExtensionArtifacts(dependsOn: buildExtensionSource, type: Copy) {
    from '../extension/LiveTL/build'
    into './src/main/assets'
    include '**/*'
}

kotlinter {
    experimentalRules = true
}
preBuild.dependsOn(formatKotlin)

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += [
        "-opt-in=kotlin.Experimental",
        "-opt-in=kotlin.RequiresOptIn",
        "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        "-opt-in=kotlinx.serialization.ExperimentalSerializationApi",
        "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
        "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
        "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
        "-opt-in=com.google.accompanist.navigation.material.ExperimentalMaterialNavigationApi",
        "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi",
    ]
}
