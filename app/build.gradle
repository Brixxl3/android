import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'dagger.hilt.android.plugin'
    id 'org.jmailen.kotlinter' version '3.13.0'
}

android {
    namespace 'com.livetl.android'

    compileSdkVersion 33

    defaultConfig {
        applicationId "com.livetl.android"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 287
        versionName "8.0.4-beta01"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'variant'
    productFlavors {
        playstore {
            dimension 'variant'
        }
//        fdroid {
//            dimension 'variant'
//            applicationIdSuffix '.fdroid'
//        }
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.7"
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'androidx.core:core-ktx:1.10.1'

    // Jetpack Compose
    implementation platform("androidx.compose:compose-bom:2023.05.01")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.material3:material3:1.2.0-alpha01"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-util"
    implementation 'androidx.navigation:navigation-compose:2.5.3'

    // Networking
    final ktor_version = '1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'

    // Image loading
    final coil_version = '2.3.0'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    final accompanist_version = '0.30.1'
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0'

    // OSS licenses
    implementation "com.mikepenz:aboutlibraries-compose:$about_libs_version"
}

// Requires the submodules to already be initialized, i.e.:
// git submodule update --init --recursive
task buildExtensionSource {
    doLast {
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "install"
        }
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "build", "android"
        }
    }
}
task copyExtensionArtifacts(dependsOn: buildExtensionSource, type: Copy) {
    from '../extension/LiveTL/build'
    into './src/main/assets'
    include '**/*'
}
project.afterEvaluate {
    tasks.findByName('mergePlaystoreReleaseAssets')?.dependsOn(copyExtensionArtifacts)
}

kotlinter {
    experimentalRules = true
}
preBuild.dependsOn(formatKotlin)

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += [
        "-opt-in=kotlin.Experimental",
        "-opt-in=kotlin.RequiresOptIn",
        "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        "-opt-in=kotlinx.serialization.ExperimentalSerializationApi",
        "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
        "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
        "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
        "-opt-in=com.google.accompanist.navigation.material.ExperimentalMaterialNavigationApi",
    ]
}
